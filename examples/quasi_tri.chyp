# This file defines quasi-triangular Hopf algebras, and proves some standard facts about them.

# A quasi-triangular hopf algebra is a Hopf algebra H:
import hopf

# With a distinguished invertible element of H (X) H...
gen r : 0 -> 2
gen ri : 0 -> 2
rule rri : r * ri ; m2 = u2
rule rir : ri * r ; m2 = u2


# Satisfying 3 extra properties. These properties are easier to state in terms
# of the left and right action of r/ri on H (X) H, defined as:
def rL = r * id * id ; m2
def rR = id * id * r ; m2
def riL = ri * id * id ; m2
def riR = id * id * ri ; m2


# The three properties are:
rule qt : n ; sw = n ; rL ; riR
rule qtL : r ; n * id = r * r ; id * sw * id ; id * id * m
rule qtR : r ; id * n = r ; id * r * id ; m * id * id

###########################################################################

# We'll introduce some extra rules the let us (un)fold rL and rR in one step:
rewrite rL_unfold :
  rL
  = r * id * id ; m2 by rL_def
  = r * id * id ; id * sw * id ; m * m by m2_def

rewrite rR_unfold :
  rR
  = id * id * r ; m2 by rR_def
  = id * r * id ; id * id * sw ; m * m by m2_def

# and commute with m2
rewrite rL_comm :
  rL * id * id ; id * sw * id ; m * m
  = id * sw * id ; m * m ; rL by simp(assoc, rL_unfold)


# r(L/R) and ri(L/R) are mutually inverse:
rewrite rL_riL :
  rL ; riL
  = id * id by simp(rL_def, riL_def, -assoc2, rir, unitL2)

rewrite riR_rR :
  riR ; rR
  = id * id by simp(rR_def, riR_def, assoc2, rir, unitR2)


# Using this fact, we can state the first quasi-triangularity law this way:
rewrite n_rL :
  n ; rL
  = n ; rL ; riR ; rR by -riR_rR
  = n ; sw ; rR by -qt


# Now, we'll show that the following "braiding" map:
let br = sw ; rL

# satisfies the famous "Yang-Baxter equation":
#
#    br * id ; id * br ; br * id = id * br ; br * id ; id * br
#


# start by considering the LHS and RHS of this equation applied to the unit:
def yb1 = u * u * u ; br * id ; id * br ; br * id
def yb2 = u * u * u ; id * br ; br * id ; id * br

# if we can prove those two states are equal, we will be able to prove the full YB equation
# by letting them both act on H (X) H (X) H:
rewrite yb1_act :
  yb1 * sw[2, 1, 0] ; m3
  = sw * id ; rL * id ; id * sw ; id * rL ; sw * id ; rL * id by simp(yb1_def, m3_def, rL_comm, unitL)

rewrite yb2_act :
  yb2 * sw[2, 1, 0] ; m3
  = id * sw ; id * rL ; sw * id ; rL * id ; id * sw ; id * rL by simp(yb2_def, m3_def, rL_comm, unitL)

# First simplify the LHS state:
rewrite yb1_simp :
  yb1
  = r * r * r ; id * sw * sw * id ; m * m * m by simp(yb1_def, rL_unfold, unitR)
  = r * r ; id * sw * id ; rL * m by -rL_unfold
  = r ; n * id ; rL * id by -qtL

# Next, simplify the RHS state:
rewrite yb2_simp :
  yb2
  = r * r * r ; sw[2, 0, 4, 1, 5, 3] ; m * m * m by simp(yb2_def, rL_unfold, unitR)
  = r * r ; id * sw[1, 2, 0] ; rR * m by -rR_unfold
  = r ; n * id ; sw * id ; rR * id by -qtL

# now we can prove the Yang Baxter rule, in "state" form
rewrite yang_baxter :
  yb1
  = r ; n * id ; rL * id by yb1_simp
  = r ; n * id ; sw * id ; rR * id by n_rL
  = yb2 by -yb2_simp

show yang_baxter

# Now prove that this holds for the map familiar "map" form the yang-baxter equations, by simplifying the left action of yb1:


# chaining these proofs together gives us a proof of the map form of the Yang Baxter equation:
rewrite yang_baxter_map :
  br * id ; id * br ; br * id
  = sw[1, 2, 0] ; yb1 * id * id * id ; id * id * id * sw * id ; m3 by -yb1_act
  = sw[1, 2, 0] ; yb2 * id * id * id ; id * id * id * sw * id ; m3 by yang_baxter
  = id * br ; br * id ; id * br by yb2_act

show yang_baxter_map


